# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname=python-carbon
_pkgname=carbon
pkgver=1.1.7
pkgrel=2
pkgdesc='Twisted daemon that listens for time-series data'
url='http://graphiteapp.org/'
arch=('any')
license=('Apache 2.0')
depends=('python' 'python-twisted' 'python-cachetools' 'python-protobuf' 'python-urllib3' 'python-six' "python-whisper>=${pkgver}")
optdepends=('python-txamqp')
makedepends=('python-setuptools')
provides=("carbon=${pkgver}")
backup=(
  etc/carbon/carbon.conf
  etc/carbon/storage-schemas.conf
  etc/carbon/storage-aggregation.conf
)
_patches=(
  '0001-Simplify-logging-to-console-stdout.patch'
)
source=(
  "${_pkgname}-${pkgver}.tar.gz::http://github.com/graphite-project/carbon/archive/${pkgver}.tar.gz"
  ${_patches[@]}
  sysusers.conf
  tmpfiles.conf
  carbon.conf
  storage-schemas.conf
  storage-aggregation.conf
  carbon-cache.service
  carbon-cache\@.service
  carbon-relay.service
  carbon-relay\@.service
  carbon-aggregator.service
  carbon-aggregator\@.service
  carbon-aggregator-cache.service
  carbon-aggregator-cache\@.service
  carbon-paranoid-security.conf
  carbon-carbon-user.conf
)
sha256sums=('8541a9c25aada40009770bac877dd730edac0ef27613509049d9ee8d5a4f5244'
            '5b220255a7d35821f3917a2899dfd17fca66eb7054375b415963369f17f1295f'
            '7f0e7ddaaa1da41d03b26882cba812d8879f38530d3bda197675039cef10c8b4'
            'a8431c6ea63efdd1a9898a94c90afcf33307c8d266c977d6e86244bfc0ddefe1'
            'c99b6084306099b3d198e851ea1002cc6b7b69cff697d98bc040d9470cd240b2'
            'a880ce74c5eb0fa7f23fe674d155e18ea8f6281083e6b1953c6660f2cc326855'
            '8527eafc996ac59dcb91649009e9e2168f2b43ec8fde01441a4697b241b83e2a'
            '25c2726b81234cd36d69e244f7d6f118c4034f229ccd793d1e7d453f48af04a9'
            '9f298bda7abe5b9ea160704058a1da6fb548dfab31b9a8a7815c1f40b8664dfc'
            '82d52a7afa4c0c0d39f0dd7d82f7c3af859390ed3db85d42d79b97702453e947'
            'a08bb135d05aab805dbee499ebb80b2b2f7c0636256a667d95f62855fb43be28'
            'fb928956ed2ca844d5f486b53e9c2b791fafd28757c876f6b4cec2e6d00307e9'
            'faae4dbf990c32e091a74eeb18e01ef8d8e1b30734218afd7c0d1118a2f21495'
            '4fada347abc554c98196c306cbb1a5991cca6275ddf5d072589b8244dc7765d0'
            'f971c97d8abed56d28eb466527bdf6693e46837df2c73fef5734a1cf332bdd99'
            '6257941d49e8e430fd41a5281835cf208aa856ca1ea140eb8c7cadc27fcd1e43'
            'f568124d9a046e777f6ce9630ee603c98f25d4ce17436aa652dbcac45592a750')

_setup_opts=(--quiet --no-user-cfg)

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  for p in ${_patches[@]}; do
    msg2 "Applying $p"
    git apply ${srcdir}/${p}
  done
}

build() { 
  cd "${srcdir}/${_pkgname}-${pkgver}"
  env GRAPHITE_NO_PREFIX=True \
      USE_SETUPTOOLS=True \
      python setup.py ${_setup_opts[@]} build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  env GRAPHITE_NO_PREFIX=True \
      USE_SETUPTOOLS=True \
      python setup.py ${_setup_opts[@]} install --root=${pkgdir} --optimize=2

  install -d -m755 ${pkgdir}/usr/share/${_pkgname}
  rm -rf ${pkgdir}/usr/storage
  rm -rf ${pkgdir}/usr/examples
  mv ${pkgdir}/usr/conf ${pkgdir}/usr/share/${_pkgname}

  # remove .py suffix
  for i in ${pkgdir}/usr/bin/*.py; do
    mv ${i} ${i%%.py}
  done

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  install -D -m644 ${srcdir}/sysusers.conf ${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf
  install -D -m644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf

  install -D -m644 ${srcdir}/carbon.conf ${pkgdir}/etc/carbon/carbon.conf
  install -D -m644 ${srcdir}/storage-schemas.conf ${pkgdir}/etc/carbon/storage-schemas.conf
  install -D -m644 ${srcdir}/storage-aggregation.conf ${pkgdir}/etc/carbon/storage-aggregation.conf

  install -d -m755 ${pkgdir}/usr/lib/systemd/system/carbon-.service.d
  for i in cache relay aggregator aggregator-cache; do
    install -D -m644 ${srcdir}/carbon-${i}.service ${pkgdir}/usr/lib/systemd/system/carbon-${i}.service
    install -D -m644 ${srcdir}/carbon-${i}\@.service ${pkgdir}/usr/lib/systemd/system/carbon-${i}\@.service
  done
  for i in carbon-user paranoid-security; do
    install -D -m644 ${srcdir}/carbon-${i}.conf ${pkgdir}/usr/lib/systemd/system/carbon-.service.d/${i}.conf
  done
}
