# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname=python-graphite-web
_pkgname=graphite-web
pkgver=1.1.6
pkgrel=2
pkgdesc="Enterprise-scale monitoring tool - web frontend an backend"
url='http://graphiteapp.org/'
arch=('any')
license=('Apache 2.0')
depends=(
  python-cairo python-urllib3 python-simplejson python-pyparsing python-whitenoise python-memcached python-pytz
  python-systemd
  'python-django<2.3' 'python-django-tagging>=0.4.6'
  uwsgi uwsgi-plugin-python
  ttf-dejavu  python-whisper 
  "carbon>=${pkgver}"
)
makedepends=('python-setuptools')
_patches=(
    '0001-Do-not-hardcode-STATICFILES_DIRS-in-settings.py.patch'
    '0002-Support-logging-to-systemd-journal.patch'
    '0003-util.logtime-raise-log-level-from-info-to-debug.patch'
    '0004-metrics.view-raise-log-level-from-info-to-debug.patch'
)
source=(
        "${_pkgname}-${pkgver}.tar.gz::http://github.com/graphite-project/graphite-web/archive/${pkgver}.tar.gz"
	${_patches[@]}
	'uwsgi-graphite.ini'
	'wsgi.py'
	'local_settings.py'
	'tmpfiles.conf'
	'graphite-web-migrate.service'
	'graphite-web-build-index.service'
	'graphite-web-collect-static.service'
	'graphite-web-paranoid-security.conf'
	'graphite-web-carbon-user.conf'
	'nginx-graphite.conf'
	'graphite-web-createsuperuser.sh'
	'graphite-web-changepassword.sh'
)

backup=(
    "etc/${_pkgname}/local_settings.py"
    "etc/${_pkgname}/uwsgi-graphite.ini"
)

sha256sums=('bd68fd243fa95e92e06d704651fb2257aba6b478a61b847090e458d94578f5df'
            '3bd995ee1e517d7c10c0dfe51a0ebfe5810b893ee4ac23449ba7b2e726964da4'
            'b579b5c4dd87013adb7b8486c599e912fdc050ef5f05c6ebb3c0a06abcdcc4c5'
            'ffa8d55996d36d52bc7ec04c928cf862c6ca0c670d8a9175cfdf8b21e7d398cf'
            '6f87c6bc0b0b34022ddce3d5d0476bfad15133afd32874a6932dbc038e082d16'
            'fb094e52711eb397cdb968a69854d0e304bc31935f9893ec652642cce5557a6a'
            '802c2be2a71339c81f564457364a5fed1bc0a314ab6e08405063d219aeab2b6e'
            '09c6d6692d66f6561f3179e9b782bf8a8977da341927cc73f9097201c87b9cfb'
            '2290e77cb667a18a7172768df068526ba54bdd94d39d8d591c0fb3632747bbf0'
            '7446d17a1ee2d8c48147072eb6dd7652d2b3ca3e95509ac29438893cd57bdd24'
            '157949a2a5da14c3328880d24af4e3e126c056cf602fae0376a01d4a5d9ba9d9'
            '07ecf36bce53dc5d7a58f53a806a561ca0cc9c290c484256205de0676f8d4d53'
            '5bb4e5bad9cf94eddd4121e275e8cd385c84207ad8b7fe1b90fd812374e45496'
            'b857710404d9778082752c02f44ea91f52d5a6d5854b9d4114dc45e1aa83d9e6'
            '6e6d7b8c46eca6ccacbb0c8bce665329e70cfdc2f480775d3c5107cc4075bf17'
            'c5b826b804d8374041ba7994271f9d7164a8c63804a62f4839dbfaac2acc9ece'
            'fc32bbfbc764c431ea89c8051c6e555856b8f8aca20d0b8a45c29ad2ebedc40e')

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    for p in ${_patches[@]}; do
      msg2 "Applying $p"
      git apply ${srcdir}/${p}
    done
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    python setup.py build
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    env GRAPHITE_NO_PREFIX=True  python setup.py install --root=${pkgdir} --optimize=2
    rm -rf $pkgdir/srv/graphite

    # additional files
    install -dm755 ${pkgdir}/etc/${_pkgname}
    install -dm755 ${pkgdir}/etc/uwsgi
    install -dm755 ${pkgdir}/usr/share/${_pkgname}
    install -Dm644 ${srcdir}/local_settings.py ${pkgdir}/etc/${_pkgname}/local_settings.py
    install -Dm644 ${srcdir}/uwsgi-graphite.ini ${pkgdir}/etc/${_pkgname}/uwsgi-graphite.ini
    install -Dm644 ${srcdir}/wsgi.py $pkgdir/usr/lib/${_pkgname}/wsgi.py
    ln -s /etc/${_pkgname}/uwsgi-graphite.ini ${pkgdir}/etc/uwsgi/graphite.ini
    mv ${pkgdir}/usr/webapp ${pkgdir}/usr/share/${_pkgname}
    mv ${pkgdir}/usr/examples ${pkgdir}/usr/share/${_pkgname}
    mv ${pkgdir}/usr/conf ${pkgdir}/usr/share/${_pkgname}
    rm -rf ${pkgdir}/usr/storage

    # local_settings.py
    for l in $(find ${pkgdir}/usr/lib -name local_settings.py.example); do
      mv ${l} ${pkgdir}/usr/share/${_pkgname}/conf
      ln -s /etc/${_pkgname}/local_settings.py ${l%.example}
      break
    done

    rm ${pkgdir}/usr/bin/run-graphite-devel-server.py
    rm ${pkgdir}/usr/bin/build-index.sh
    mv ${pkgdir}/usr/bin/build-index ${pkgdir}/usr/lib/${_pkgname}/build-index
    install -Dm755 ${srcdir}/graphite-web-createsuperuser.sh ${pkgdir}/usr/bin/graphite-web-createsuperuser
    install -Dm755 ${srcdir}/graphite-web-changepassword.sh ${pkgdir}/usr/bin/graphite-web-changepassword

    # nginx - configuration
    install -Dm644 ${srcdir}/nginx-graphite.conf ${pkgdir}/usr/share/${_pkgname}/conf/nginx-graphite.conf

    # tmpfiles
    install -Dm644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf

    # systemd
    for s in migrate build-index collect-static; do
      install -Dm644 ${srcdir}/${_pkgname}-${s}.service ${pkgdir}/usr/lib/systemd/system/${_pkgname}-${s}.service
    done
    for s in paranoid-security carbon-user; do
      install -Dm644 ${srcdir}/${_pkgname}-${s}.conf ${pkgdir}/usr/lib/systemd/system/${_pkgname}-.service.d/${s}.conf
    done
}
