# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname=python-whisper
_pkgname=whisper
pkgver=1.1.7
pkgrel=2
pkgdesc='Database library for storing time-series data'
url='http://graphiteapp.org/'
arch=('any')
license=('Apache 2.0')
depends=('python' 'python-cachetools' 'python-protobuf' 'python-twisted')
makedepends=('python-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::http://github.com/graphite-project/whisper/archive/${pkgver}.tar.gz")
sha256sums=('7a55a13e2096c97d8dbb2ea225bf29e7296222d0af00c289d98fb592c36201ba')
_setup_opts=(--quiet --no-user-cfg)

build() { 
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py ${_setup_opts[@]} build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py ${_setup_opts[@]} install --root=${pkgdir} --optimize=2

  # remove .py suffix
  for i in ${pkgdir}/usr/bin/*.py; do
    mv ${i} ${i%%.py}
  done

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
